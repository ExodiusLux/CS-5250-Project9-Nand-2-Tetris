class Main {
  function void main() {
    var boolean game_ongoing;
    var boolean guessed_correctly;
    var boolean word_complete;
    var char character_guess;
    var int current_word_length;
    var int i;
    var int incorrect_guesses;
    var Array guessed_word;
    var String current_word;
    var Hangman hangman;
    var Dictionary hangman_words;
    var boolean word_complete;
    var int turn_counter;

    let hangman_words=Dictionary.new();
    let hangman=Hangman.new();
    let game_ongoing=true;
    let incorrect_guesses=0;
    let current_word=hangman_words.getWord(Keyboard.readInt("Enter a number between 0 and 15 inclusive: "));
    let current_word_length=current_word.length();
    let guessed_word=Array.new(current_word_length);
    
    //fill array with _ (what user will interact with to find the word)
    let i = 0;
    while(i < current_word_length) {
      let guessed_word[i] = 95; //95 is ascii code for _ which is our placeholder for unknown word
      let i = i + 1;
    }

    let i = 0;
    do Output.printString("Enter the first character to guess, afterwards continue entering characters (do not hit enter):   ");
    let turn_counter = 0;
    while(game_ongoing) {
      if(turn_counter = 10){
        do Screen.clearScreen();
        do hangman.redraw();
      }
      let word_complete = true;
      let guessed_correctly = false;
      
      let character_guess = Keyboard.readChar();
      do Output.println();
      let i = 0;

      //check the word to see if the guess is in it
      while(i < current_word_length) {
        if(character_guess = current_word.charAt(i)) {
          let guessed_word[i] = character_guess; //replace the _ with the guessed character
          let guessed_correctly = true;
        }
        let i = i+1;
      }
      //check current progress
      do Output.printString("current progress: ");
      let i=0;
      while(i < current_word_length){
        do Output.printChar(guessed_word[i]);
        let i = i + 1;
      }

      let i = 0;

      //if character was not in the word
      if(~guessed_correctly) {
        let incorrect_guesses = incorrect_guesses+1;
        do hangman.drawNextPart();
        if(incorrect_guesses = 7) {
          do Screen.clearScreen();
          do hangman.drawWholeMan();
          do Output.printString("Uh oh! You guessed incorrectly too many times.");
          do Output.println();
          let game_ongoing = false;
        }
      }
      //check if guess array has any _ left (word still unfinished) 
      while(i < current_word_length) {
        if(guessed_word[i] = 95){
          let word_complete = false;
        }
        let i = i + 1;
      }

      //if word has been completely guessed
      if(word_complete) {
        do Screen.clearScreen();
        do Output.printString("You got the word correct! Game will now end!");
        do Output.println();
        let game_ongoing = false;
      }

      //reset guessed_correctly flag
      let guessed_correctly = false;
      do Output.println();
      let turn_counter = turn_counter + 1;
    }
    //game over
    do guessed_word.dispose();
    do current_word.dispose();
    
    return;
  }
}